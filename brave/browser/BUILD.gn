import("//build/buildflag_header.gni")
import("//build/config/chrome_build.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/features.gni")
import("//extensions/features/features.gni")
import("//ppapi/features/features.gni")
import("//printing/features/features.gni")

if (is_win) {
  # This is in a separate config so the flags can be applied to dependents.
  # ldflags in GN aren't automatically inherited.
  config("browser_win_linker_flags") {
    libs = [
      "credui.lib",
      "cryptui.lib",
      "dwmapi.lib",
      "netapi32.lib",
      "ndfapi.lib",  # Used by browser/net/net_error_diagnostics_dialog_win.h
      "pdh.lib",  # Used by browser/private_working_set_snapshot.h
    ]
    ldflags = [
      "/DELAYLOAD:ndfapi.dll",
      "/DELAYLOAD:pdh.dll",  # The task manager is rarely used.
    ]
  }
}


source_set("browser") {
  configs += [
    "//electron/build:electron_config",
  ]

  include_dirs = [
    # make sure chromium_src comes before the chrome src root
    "//electron/chromium_src",
  ]

  sources = [
    # "api"
    "guest_view/tab_view/tab_view_guest.h",
    "guest_view/tab_view/tab_view_guest.cc",
    "guest_view/brave_guest_view_manager_delegate.h",
    "guest_view/brave_guest_view_manager_delegate.cc",
    "notifications/platform_notification_service_impl.h",
    "notifications/platform_notification_service_impl.cc",
    "password_manager/brave_password_manager_client.h",
    "password_manager/brave_password_manager_client.cc",
    "password_manager/brave_credentials_filter.h",
    "password_manager/brave_credentials_filter.cc",
    "//chrome/browser/browsing_data/browsing_data_helper.h",
    "//chrome/browser/browsing_data/browsing_data_helper.cc",
    "//chrome/browser/mac/security_wrappers.h",
    "//chrome/browser/mac/security_wrappers.cc",
    "//chrome/browser/password_manager/password_store_factory.h",
    "//chrome/browser/password_manager/password_store_factory.cc",
    "//chrome/browser/password_manager/password_store_proxy_mac.h",
    "//chrome/browser/password_manager/password_store_proxy_mac.cc",
    "//chrome/browser/password_manager/password_store_mac.h",
    "//chrome/browser/password_manager/password_store_mac.cc",
    "//chrome/browser/password_manager/simple_password_store_mac.h",
    "//chrome/browser/password_manager/simple_password_store_mac.cc",
    "//chrome/browser/password_manager/password_manager_util_win.h",
    "//chrome/browser/password_manager/password_manager_util_win.cc",
    "//chrome/browser/password_manager/password_store_win.h",
    "//chrome/browser/password_manager/password_store_win.cc",
    "brave_browser_context.h",
    "brave_browser_context.cc",
    "brave_content_browser_client.h",
    "brave_content_browser_client.cc",
    "brave_javascript_dialog_manager.h",
    "brave_javascript_dialog_manager.cc",
    "brave_permission_manager.h",
    "brave_permission_manager.cc",
    "certificate_viewer_mac.mm",
    "renderer_preferences_helper.h",
    "renderer_preferences_helper.cc",
  ]

  public_deps = [
    "//components/autofill/core/browser",
  ]

  deps = [
    ":component_updater_api",
    "//electron/atom/browser",
    "//electron/chromium_src:browser",
    "//electron:common",
    "//chrome/common",
    "//components/autofill/content/browser:risk_proto",
    "//components/autofill/content/browser",
    "//components/autofill/core/browser",
    "//components/password_manager/content/browser",
    "//components/password_manager/core/browser",
    "//components/password_manager/core/common",
    "//components/resources",
    "//components/subresource_filter/content/browser",
    "//components/subresource_filter/core/browser",
    "//components/sync_preferences",
    "//content/public/common:service_names",
    "//device/usb/mojo",
    "//device/usb/public/interfaces",
    "//gpu/config",
    "//mojo/common",
    "//mojo/edk/system",
    "//mojo/public/cpp/bindings",
    "//mojo/public/js",
    "//third_party/WebKit/public:image_resources",
    "//third_party/WebKit/public:resources",
  ]

  if (is_win) {
    all_dependent_configs = [ ":browser_win_linker_flags" ]
  }

  if (enable_basic_printing || enable_print_preview) {
    deps += [
      "//components/printing/browser",
      "//printing",
    ]
  }

  if (enable_plugins) {
    sources += [
      "plugins/brave_plugin_service_filter.cc",
      "plugins/brave_plugin_service_filter.h",
    ]
  }

  if (enable_extensions) {
    public_deps += [
      "extensions",
    ]

    deps += [
      "//chrome/common/extensions/api",
      "//chrome/common/extensions/api:api_registration",
      "//chrome/common/extensions/api:extensions_features",
      "//extensions/browser",
    ]
  }

  if (is_posix && !is_mac) {
    sources += [
      # "//chrome/app/chrome_crash_reporter_client.cc",
      # "//chrome/app/chrome_crash_reporter_client.h",
    ]
    deps += [
      "//components/crash/content/app",
      "//components/crash/content/browser",
      "//components/version_info:generate_version_info",
    ]
  }
}

source_set("tab_manager") {
  configs += [
    "//electron/build:electron_config",
  ]

  sources = [
    "memory/guest_tab_manager.cc",
    "memory/guest_tab_manager.h",
  ]

  deps = [
    "//content/public/common",
    "//electron/chromium_src:tab_manager"
  ]
}

source_set("component_updater_api") {
  configs += [
    "//electron/build:electron_config",
  ]

  include_dirs = [
    # make sure chromium_src comes before the chrome src root
    "//electron/chromium_src",
  ]

  sources = [
    "api/brave_api_component_updater.cc",
    "api/brave_api_component_updater.h",
    "api/brave_api_extension.cc",
    "api/brave_api_extension.h",
  ]

  deps = [
    "//electron/build/node",
    "//v8:v8",
    "//v8:v8_libplatform",
    "//third_party/WebKit/public:blink_headers",
  ]

  public_deps = [
    "component_updater",
  ]
}
